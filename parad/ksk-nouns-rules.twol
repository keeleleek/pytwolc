CoS = :b|:d|:h|:k|:l|:m|:n|:p|:r|:s|:t|:v | j ;
    ! all surface consonants (does not contain {ij}:j) 
CoM = [[CoS].u .o. :] ;
    ! all morphophonemic consonants
    ! all X:Y where for some Z, X:Z is in CoS
CoØ = [CoM - CoS] ;
VoS = :a | :e | :i | :o | :u | :y | :ä | :ö ;
    ! all surface vowels
Vok = [VoS - {ij}:] ;
VoM = [[VoS].u .o. :] ;
VoØ = [VoM - VoS] ;
MeSØ = [[:e].u] .o. :Ø ;
    ! all X:Ø where X:e in :
VV = [:a  :Ø* :a|:e :Ø* :e|:i :Ø* :i|:o :Ø* :o|:u :Ø* :u|
      :y :Ø* :y|:ä :Ø* :ä|:ö :Ø* :ö] ;
VVØ = [:a :Ø* [:a & :Ø]] | [:e  :Ø* [:e & :Ø]] | [:i  :Ø* [:i & :Ø]] |
      [:o  :Ø* [:o & :Ø]] | [:u :Ø* [:u & :Ø]] | [:y :Ø* [:y & :Ø]] |
      [:ä :Ø* [:ä & :Ø]] | [:ö :Ø* [:ö & :Ø]] ;
VbV = [:e :a|:e :ä|:i :a|:i :o|:i :ö|:o :a|:u :a|:u (:Ø) :e|:y (:Ø) :e] ;
CLOSED = VoM ({ij}:i|{ØiØ}:) CoM [(VoØ) CoM |:Ø* .#.] ;
OPEN = VoS ((VoØ) {ij}:i|{ØiØ}:) [CoM VoS|:Ø* .#.] ;

{V}:a <=> :a :Ø* ({Øh}:) _ ; ! maah<a>n
{V}:e <=> :e :Ø* ({Øh}:) _ ; ! nalle<e>n
{V}:i <=> :i :Ø* ({Øh}:) _ ; ! risti<i>n
{V}:o <=> :o :Ø* ({Øh}:) _ ; ! valo<o>n
{V}:u <=> :u :Ø* ({Øh}:) _ ; ! parru<u>n
{V}:y <=> :y :Ø* ({Øh}:) _ ; ! kyky<y>n
{V}:ä <=> :ä :Ø* ({Øh}:) _ ; ! pääh<ä>n
{V}:ö <=> :ö :Ø* ({Øh}:) _ ; ! yöh<ö>n

{aØ}:Ø <=> _ [{ij}: | t] ; ! ma<a> ma<>issa, koir<>ia, jumal<>ten
{ao}:o <=> _ {ij}: ; ! kair<a> kair<o>ja 
{aØo}:o => _ [{ij}:j | {ij}:i :Ø* CoS] ; ! perun<o>ita
{aØo}:Ø => _ {ij}:i ; ! perun<>ia ** toghether with GEN & PTV 
{aØo}:a /<= _ {ij}: ; ! perun<a>

{aä}:a <=> [:a|:o|:u] :* _ ; ! maass<a>, piiss<ä>

{ØaØØØ}:Ø|{ØäØØØ}:Ø =>
    [:h|:n|:t|{sØh}:|:n {ØpmØ}:Ø] _
        ({nØØØ}:) [{Øt}:|:t|{nrs}:] ,
    _ ({nØØØ}:Ø) [{ij}:|.#.] ;
{ØaØØØ}:a|{ØäØØØ}:ä /<=
    [:h|:n|:t|{sØh}:|:n {ØpmØ}:Ø] _
        ({nØØØ}:) [{Øt}:|:t] ,
    _ ({nØØØ}:Ø) [{ij}:|.#.] ;
    ! pahin<> pahimm<a>n pahin<>ta (pahin<>na)
    ! muudan<> muutam<a>n muuat<>ta (muuan<>na)
    ! saapa<>s saappa<a>n saapa<>sta
    ! vieras<> viera<a>n vierah<a>n vieras<>ta (vieras<>na)
    ! lämmin<> lämpim<ä>n lämmin<>tä (lämmin<>nä) lämpim<>iä
    ! kevät<> kevä<ä>n kevät<>tä (kevän<>nä) kevä<>itä

{eeØ}:Ø <=> _ [{ij}:] ;
    ! leve<e> leve<e>tä leve<>itä
    ! kame<e> kame<e>ta kame<>ita

{ØeØØØ}:e /<= _ [:t|{ij}:|.#.] ;
{ØeØØØ}:Ø =>  _ [:t|{ij}:|.#.] , VoS (CoØ) [:s|:n] _ {nrs}: ;
    ! ahven<> ahven<e>n ahven<>ta ahven<e>na ahven<>na
    ! airut<> airu<e>n airut<>ta airu<e>na
    ! terve<> terve<e>n tervet<>tä terve<e>nä terveh<e>nä terven<>nä

{eeØØ}:Ø => _ {nØØØ}:Ø [{ij}:|:t|{nrs}:] ;
{eeØØ}:e /<= _ {nØØØ}:Ø [{ij}:|:t] ;
    ! hevon<e>n hevos<e>n hevos<>ta hevos<e>na hevos<>sa hevos<>ia

{Øe}:Ø <=> :h (:Ø) VoS s e _ ;
    ! kaunihise<>n vaatteese<e>n vaattehese<>n vaattehisi<>n 

{ØeØeØ}:Ø <=> _ .#.|{ij}:|:t ;
    ! vastaus<> vastauks<e>n vastaus<>ta vastauks<e>na vastauks<>ia
    ! uistin<> uistim<e>n uistin<>ta uistim<>ia
    ! askel<> askel<e>n askel<>ta askel<e>na askel<>ia
    ! sisar<> sisar<e>n sisar<>ta sisar<e>na sisar<>ia
    ! tuha<>t tuhann<e>n tuhat<>ta tuhant<e>na tuhans<>ia
    ! kalleus<> kalleud<e>n kalleut<>ta kalleut<e>na kalleuks<>ia

{Øh}:Ø => CoS VoS ({nØØØ}:Ø) _ , VbV _ , CoS VoØ ({nØØØ}:Ø) {ij}:i _ ;
    ! talo<>on valtio<>on hevose<>en hevosi<>in

{ØØh}:h => o _ [:o [:n|:s]|VoØ {ij}: [:s|:t|VoS CoS|{Øt}:Ø :a]] ,
           a _ [:a [:n|:s]|VoØ {ij}: [:s|:t|VoS CoS|{Øt}:Ø :a]] ,
	   e _ [:e [:n|:s]|VoØ {ij}: [:s|:t|VoS CoS|{Øt}:Ø :a]] ,
	   a _ CoØ [:a [:n|:s]|VoØ {ij}: [:s|:t|VoS CoS|{Øt}:Ø :a]] ;
    ! vapa<>a vapa<h>an vapa<h>isin
    ! tieno<>o tieno<h>on tieno<h>isin
    ! ??korke<>e tieno<>o koira<h>an
    ! NOTE: some overgenerated forms remain

{Øth}:h => _ [:e|:i|:u] [:n|:s],_ VoØ {ij}:i [e|s|:t|{Øt}: {aä}:];
{Øth}:t <=> _ VoØ [{Øt}:|:t] ;
    ! vaatte<h>en vaatte<h>esen vaatte<h>ia vaatte<h>isin
    ! ori<h>in ori<h>isen
    ! kiiru<t>ta kiiru<h>un kiiru<h>isin
    ! vaate<t>ta ori<t>ta
    ! askele<> askele<t>ta

{ØhØtØØ}:h => _ {ØeØeØ}:e :n .#. ;
    ! askele<h>en
{ØhØtØØ}:t => _ {ØeØeØ}:Ø {Øt}:t {aä}: ;
    ! askele<t>ta
{ØhØtØØ}:Ø => _ {ØeØeØ}:Ø .#.|{ØeØeØ}:e|{ØeØeØ}:Ø {ij}: ;
    ! askele<> askele<t>ta askele<>na askele<>ita

{iieØ}:Ø <=> _ {ij}: e ;
{iieØ}:e => _ [{ij}:|:i] ;
{iieØ}:i /<= _ [{ij}:|i] ;
    ! paperi<i> paperi<i> paper<e>j paper<>ien

{ØieØ}:Ø <=> _ :Ø* .#. ,
        _ {ij}: e ;
{ØieØ}:e => _ [{ij}:|i] ;
    ! kalsium<> kalsium<i>a kalsium<e>ja kalsium<>ien

{ieeeØ}:i <=> _ :Ø* .#. ;
{ieeeØ}:Ø <=> _ {ij}: ;
    ! tupp<i> tup<e>n tupp<e>a tupp<e>na tupp<>ia tupp<>ien

{ieeØeØ}:i <=> _ :Ø* .#. ;
{ieeØeØ}:Ø => _ {ij}: ,
        [:h :Ø*|:l|:n|:r|VoS :Ø* :s :Ø*|:t] _ [{Øt}:|:t] ;
{ieeØeØ}:e /<= _ {ij}: ;
{ieeØeØ}:e /<= [:l|:n|:r|VoS :s|:t] _ [{Øt}:|:t] ;
    ! niem<i> niem<e>n niem<e>ä nien<>tä niem<e>nä niem<>iä
    ! lum<i> lum<e>n (lum<e>a) lun<>ta lum<e>na lum<>ia
    ! suks<i> suks<e>n suks<e>a (sus<>ta) suks<e>na suks<>ia

{ieØeØ}:i <=> _ :Ø* .#. ;
{ieØeØ}:Ø <=> _ [{ij}: | :t] ;
    ! käs<i> käd<e>n kät<>tä kät<e>nä
    ! loh<i> loh<e>n loh<>ta loh<e>na loh<>ia

{ieØeØØ}:i <=> _ .#. ;
{ieØeØØ}:Ø => _ [{ij}: | :t] ,
        VoS (CoØ) [:s|:r|:n] _ {nrs}: ;
{ieØeØØ}:e /<= _ [{ij}:|:t] ,
        VoS (CoØ) CoS _ {Øt}: ;
    ! pien<i> pien<e>n pien<>tä pien<e>nä pien<>nä pien<>iä
    ! laps<i> laps<e>n las<>ta laps<e>na las<>na
    ! haps<i> haps<e>n haps<e>a has<>ta haps<e>na has<>na haps<>ia
    ! nuor<i> nuor<e>n nuor<>ta nuor<e>na (nuor<>na nuor<>ra) nuor<>ia


{iØ}:Ø <=> _ (e) {ij}: ,
        :h VoS s e _ ,
	:h (:Ø) VoØ {ij}:i s i _ ;
    ! t<i>e t<>eitä
    ! kame<e> kame<>ita
    ! vapaisi<i>in vapahisi<>n

{ØiØ}:i => _ :n VoØ [{nrs}: | :t | .#. ] ;
    ! onneto<>n onneto<i>n onneto<>nta onneto<i>nta onneto<i>nna

{ØiØiØ}:Ø => _ {ij}:|{nrs}:|:t|.#. ;
{ØiØiØ}:i /<= _ {ij}:|:t|.#. ;
    ! kauris<> kauri<i>n kauri<>sta kauri<i>na kauri<>sna kauri<>ita
    ! kauri<i>seen kaurih<i>sen kauri<i>ta kauri<>hia

{ij}:i => CoS :Ø* _ , _ :Ø* CoS ;
{ij}:j => VoS _ :Ø* VoS ;
    ! talo<i>ssa talo<j>a

{kØ}:Ø <=> _ CLOSED ;
    ! ark<k>u ark<>un

{kØØ}:Ø | {tØØ}:Ø => _ CLOSED ,
        _ [:o|:u|:y|:ö] {ij}:i [:t|:h] ;
{kØØ}:k | {tØØ}:t /<= _ CLOSED ,
        _ [:o|:ö] {ij}:i :t ;
    ! mansik<k>a mansik<>an mansik<>oita mansik<k>oja mansik<>oihin
    ! mansik<k>oihin mansik<k>oja mansik<>oita *mansik<k>oita
    ! ilmet<t>ty ilmet<t>yjä ilmet<>yitä *ilmet<t>yitä
    ! sisäk<k>kö sisäk<>öihin sisäk<k>öihin

{kh}:k <=> _ :s ;
    ! ka<k>si ka<h>den ka<h>ta

{mn}:m <=> _ :Ø* [VoS | :m | :p] ;
    ! lu<m>i lu<m>en lu<n>ta lu<m>ena lu<m>ia
    ! tuo<m>i tuo<m>en tuo<m>ea (tuo<n>ta) tuo<m>ia
    ! nie<m>i nie<m>en nie<m>eä nie<n>tä
    ! uisti<n> uisti<m>en uisti<n>ta
    ! onneto<n> onnetto<m>an onneto<n>ta onneto<n>na
    ! lämmi<n> lämpi<m>än lämmi<n>tä lämpi<m>änä lämpi<m>iä
    ! pahi<n> pahi<m>man vase<m>man seitse<m>än

{nrs}:r => :r VoØ _ ; 
{nrs}:s => :s VoØ ({nØØØ}:Ø) _ ;
    ! nuor<r>a nuore<n>a nuor<n>a
    ! las<n>a lapse<n>a las<s>sa

{ns}:n <=> _ :e :n ;
    ! hevo<n>en hevo<s>en hevo<s>ta hevo<s>ena hevo<s>na hevo<s>ia

{nØØØ}:n => _ .#. ;
    ! hevone<n> hevose<>n hevos<>ta hevos<>ia

{oØ}:Ø <=> _ {ij}: ;
    ! tieno<o> tieno<o>ta tieno<>ita aino<o>

{pØ}:Ø|{pm}:m <=> _ CLOSED ;
    ! tup<p>i tup<>en saap<>as saap<p>aan
    ! läm<m>in läm<p>imän läm<m>intä

{pØp}:Ø <=> _ s {ieØeØØ}:Ø [{nrs}: | :t] ;
    ! la<p>si la<p>sen la<>sta la<p>sena la<>sna la<>ssa la<p>sia
    ! ha<p>si ha<p>sen ha<>sta ha<p>sena ha<>sna ha<>ssa ha<p>sia

{shs}:s => _ VoØ [{nrs}:|{Øt}:|:t|.#.] ;
{shs}:h /<= _ VoØ [{Øt}:|:t|.#.] ;
    ! mie<s> mie<h>en mie<s>tä mie<h>enä mie<h>iä

{sØh}:s <=> _ VoØ [:t | {nrs}: | .#.] ;
{sØh}:h => _ VoS [:n|:s] ,
        _ VoØ {ij}:i [(:t :t) e n|{Øt}:Ø {aä}:|s] ;
    ! viera<s> viera<>an (viera<h>an) viera<s>ta
    ! viera<>ana (viera<s>na) (viera<h>ana) (viera<h>isin)
    ! uro<s> uro<>on uro<h>on uro<s>ta uro<>oseen uro<h>osen uro<s>ten

{snt}:s <=> _ (VoØ) [:i | .#.] ;
    ! kahdeksa<s> kahdeksan<s>ia
{snt}:n => _ CLOSED ;
    ! kahdeksan<n>en kahdeksan<n>essa
{snt}:t => _ OPEN ;
    ! kahdeksan<t>ena kahdeksa<t>ta

{tØ}:Ø <=> _ CLOSED ;
    ! pat<t>i pat<>in 

{td}:d <=> _ CLOSED ;
    ! va<t>i va<d>in

{tds}:s|{tdsØ}:s|{tls}:s|{trs}:s|{tns}:s <=> _ (VoØ) [:i|:Ø* .#.] ;
{tds}:d|{tdsØ}:d|{tls}:l|{tns}:n|{trs}:r <=> _ :e CoM (VoØ) [CoM|:Ø* .#.] ;
    ! to<s>i kä<s>i kä<s>iä kak<s>i jäl<s>i kan<s>i kahdeksa<s>
    ! kä<d>en kä<d>essä kah<d>en jäl<l>en kan<n>essa
    ! kan<s>i kan<s>ia kan<n>en kan<t>ta kan<t>ten kan<s>ia
    ! hir<s>i hir<t>enä hir<r>essä hir<s>iä

{tdsØ}:Ø <=> _ VoØ :t ;
    ! kah<>ta

{ØØthn}:n => _ VoØ {nrs}: ;
{ØØthn}:t <=> _ VoØ [:t] ;
    ! terve<> (terve<h>en) (terve<n>nä) terve<t>ä (terve<t>ten)

{tØthn}:t <=> _ VoØ [:t | .#.] ;
{tØthn}:n => _ VoØ {nrs}: ;
{tØthn}:h => _ [VoS-{ij}:] ,
             _ VoØ {ij}:i [e|:s|:t|e|{Øt}:Ø {aä}:] ;
{tØthn}:Ø => _ VoØ {ij}: , _ VoS ;
    ! kuollu<t> kuolle<>en kuolle<h>en kuollu<t>ta kuolle<>ena
    ! kuolle<h>en kuolle<>ita (kuollu<t>ten) kuolle<>isiin kuolle<h>isin
    ! kevä<t> kevä<>än kevä<h>än kevä<t>tä
    ! kevä<>änä kevä<h>änä (kevä<n>nä)
    ! kevä<>itä (kevä<h>itten) kevä<>isiin
    ! aru<t> airu<>en airu<t>ta airu<>ena airu<>ita
    ! airu<h>itten airu<h>ien airu<>isiin (airu<h>isin)
    ! äity<h>itten äity<h>ien (äity<h>isin)

{tØt}:Ø => _ s VoØ :t ;
    ! pei<t>si pei<>stä pei<t>sillä pei<>sten

{uØ}:Ø <=> _ (o) {ij}: ;
    ! s<u>o s<>oita tenkkapo<o> tenkkapo<>ita

{ue}:u <=> _ [:t|:n] ;
    ! kuoll<u>t kuoll<u>tta kuoll<u>nna

{ØuØuØ}:Ø <=> _ [:t|{ij}:|:Ø* .#.] ;
    ! kiiru<> kiiru<u>n kiirut<>ta kiiru<u>na kiiruh<u>na

{Øt}:t => Vok CoØ* VoØ {ij}:| Vok CoØ* Vok ({ij}:) _ ,
        VoØ Vok {ij}:i _ ,
        VoØ CoØ* _ ,
	{dt}:t _ ,
	.#. CoM* [Vok+ CoM+]+ Vok+ CoM+ [:a|:ä] (CoØ) _ ,
	.#. CoM* [Vok+ CoM+]+ Vok+ CoM+ Vok {ij}:i _ ;
    ! maa<t>a mai<t>a ainoo<t>a ainoi<t>a autio<t>a autioi<t>a
    ! soi<t>a töi<t>ä tei<t>ä
    ! tuoh<t>a loh<t>a lun<t>a nien<t>tä pien<t>ä nuor<t>ta
    ! kät<t>ä tot<t>a hirt<t>tä jält<t>ä kant<t>ta
    ! las<>ta has<t>a veis<t>ä peis<t>ä sus<t>ta us<t>a
    ! sisar<t>a uistin<t>a lämmin<t>ä vasen<t>ta
    ! hevos<t>a vastaus<t>a
    ! vieras<t>a saapas<t>a
    ! arvelui<t>a ilmetyi<t>ä kantaja<t>a pasuuna<t>a

{Øt}:Ø => CoS VoS (CoØ) ({ij}:j) _ ,
        CoS (CoØ) VoØ (CoØ) {ij}:i _ ,
	:e :a|:e :ä|:i :o|:i :ö|:o :a|:u :a _ ,
        {dt}:d _ ;
    ! arkku<>a arkkuj<>a pato<>a patoj<>a seitsem<>ä
    ! arvelu<>a arveluj<>a ilmetty<>ä ilmettyj<>ä
    ! sisäkkö<>ä sisäkköj<>ä
    ! ahveni<>a
    ! maid<>en

!!!{Øt}:Ø /<= [{ieØeØ}:Ø|{ieØeØ}:Ø|
!!!            {ØeØeØ}:|{ØaØØØ}:Ø|{ØäØØØ}:Ø] ({ØnØ}:Ø) _ ;
{Øt}:Ø /<= [VV |VVØ {ij}:| VoS VoS {ij}:] _ ;
    ! *maa<>a maa<t>a *ainoo<>a ainoo<t>a
    ! *autioj<>a autioi<t>a
{Øt}:Ø /<= [{uØ}: o|{iØ}: e] ({ij}:) _ ;
    ! *suo<>a suo<t>a *soi<>a soi<t>a tei<t>ä
{Øt}:t /<= CoS [:o|:u|:y|:ö|{iieØ}:|{ØieØ}:|{iieØ}:|:e] _ ;
    ! *talo<t>a *risti<t>ä kalsiumi<t>a valo<>a valoj<>a *arvelu<t>a
    ! lovi<>a hevosi<>a, (autio<>a) herttua<>a 
{Øt}:t /<= .#. CoS* VoS+ [CoS|CoØ]+ [{aØ}:a|{ao}:a] _ ;
    ! *koira<t>a *kissa<t>a 
{Øt}:t /<= .#. CoS* VoS+ [CoS|CoØ]+ [VoS|VoØ] {ij}: _ ;
    ! *arkkui<t>a arkku<>ja *vatei<t>a vatej<>a *käsi<t>ä käsi<>ä
    ! (kantaji<t>a) *valoi<t>a valoj<>a 
{Øt}:t /<= CoS VoØ ({nØØØ}:Ø) {ij}: _ ;
    ! *hevosi<t>a hevosi<>a
    ! vapaa<t>a ainoo<t>a
    ! arvelui<t>a loh<t>a autio<t>a, hevos<t>a sisar<t>a,
    ! autio<t>a leveä<t>ä, kulkija<t>a seitsemä<t>ä

s e {Øe}: n => [VV |
                [:a :h (:Ø) :a|:e :h (:Ø) :e|:i :h (:Ø) :i|:o :h (:Ø) :o|
                 :u :h (:Ø) :u|:y :h (:Ø) :y|:ä :h (:Ø) :ä|:ö :h (:Ø) :ö]
	       ] _ :Ø* .#. ;
    ! ainoo<seen> vapaa<seen> korkee<seen>
    ! (vapaha<sen>) (koiraha<sen>)

{ij}:i s i {iØ}: n => [VoS (:h|:Ø) (:Ø) VoØ] _ ;
    ! vapa<isiin> aino<isiin> (ainoh<isin>)
    ! saappa<isiin> (saappah<isin>) koira<isiin> (koirah<isin>

{ij}: e n /<= VVØ _ ;
    ! valo<jen> *tie<jen> *maa<jen> käs<ien> arvelu<jen>

{ij}: e n /<= VoS :Ø* VoØ _ ;
    ! valo<jen> käs<ien> *te<jen> ma<jen> 

{ij}: {dt}: /<= [CoS - :h] :Ø* _ ;
    ! *käs<iden> *koir<iden>

{ij}: {dt}: /<= .#. CoS* VoS+ [CoS|CoØ]+ VoS _ ;
    ! *valo<iden> *kisso<iden>

{dt}:t <=> _ :t ;
    ! mai<d>en mai<t>ten

t e n /<= VoS :Ø* _ :Ø* .#. ;
    ! *arvelu<ten> risti<ten> *ma<ten> *tie<ten>

i n /<= [VV] _ :Ø* .#. ;
    ! (valo<in>) (autio<in>) (vate<in>)
    ! *maa<in> *tie<in> *suo<in>

STOP

****************************************

{ØaØa}:Ø => _ [{ij}: | :t |.#.] ; ! koiras<> koira<a>n koiras<>ta
{ØaØa}:a /<= _ [{ij}:|.#.] , :s _ ;
        ! koira<>issa koiras<>ta vasen<> vasemp<a>ta

{ØiØØ}:Ø =>  _ [{ij}: | :t | .#.] , :s _ {nrs}: ;
{ØiØØ}:i /<=  _ [{ij}: | :t | .#.] ;
        ! kauris<> kauris<>ta (kauris<>na)
	! kauri<i>na  kaurih<i>n

{ØiØi}:Ø <=> _ [:t | {ij}: | .#.] ;
        ! ori<> ori<i>n orit<>ta ori<i>na ori<>ita

{iiee}:e => _ {sØh}: VoØ {ij}: ;
        ! kaun<e>issa (kaun<e>hien)
        ! kaun<i>s kaun<i>in kaun<i>sta kaun<i>ina

{iiØie}:Ø <=> _ [:t|{ij}: e] ; ! (sankar<>ten) sankar<>ien
{iiØie}:e => _ [:i|{ij}:]; ! sankar<e>ita (sankar<e>in)
{iiØie}:i /<= _ [:i|{ij}:]; ! sanka<i> sankar<i>a sankar<i>na

{kkØ}:Ø <=> _ s VoØ :t ;
        ! su<k>si su<k>sea su<>sta u<k>si u<>sta

{ØkØ}:Ø <=> _ s VoØ [:t | .#.] ;
        ! vastau<>s vastau<k>sen vastau<>sta vastau<k>sena

{ØØk}:k <=> _ :s VoØ {ij}: ;
        ! kalleu<>s kalleu<>den kalleu<>tta kalleu<k>sia

{ØkØØ}:Ø <=> _ s VoØ [:t | {nrs}: | .#.]; ! koira<>sta (koira<>sna)
        ! koira<k>sen koira<k>sena koira<k>sia

{mnt}:n <=> _ VoØ [{nrs}: | .#.] ;
{mnt}:t <=> _ VoØ :t ;
        ! muua<t>ta muua<n> (muua<n>na)

{ØnØ}:Ø <=> _ :t VoØ [:t|.#.], _ :n VoØ :t, _ [:n|:s] VoØ [{nrs}:|.#.];
        ! tuha<>t tuha<n>en tuha<>tta tuha<n>tena tuha<n>sia
	! kahdeksa<>s kahdeksa<n>nen kahdeksa<>tta khdeksa<n>tena

{nØØ}:n <=> _ .#. ; ! hevone<n> hevose<>ssa seitsemä<n> seitsemä<>ssä

{nss}:n <=> _ :e :n ; ! hevo<n>en hevo<s>en hevo<s>ta hevo<s>ia

{ØoØo}:Ø => _ [:t|{ij}:|:i|.#.] ;
{ØoØo}:o /<= _ [:t|{ij}:|:i|.#.] ;
        ! uro<>s uro<>sta uro<o>n uro<o>na (uroh<o>na)

{Øpm}:Ø <=> _ VoØ [:t | .#.] ;
{Øpm}:m <=> _ CLOSED ;
        ! vasen<> vasen<>ta vasem<p>ana vasem<p>ia vasem<m>an vasem<m>issa

{ppØ}:p <=> _ s :Ø* VoS ;
        ! la<p>si la<p>sen la<>sta la<p>sia

{ØpmØ}:Ø <=> _ VoØ [:t | {nrs}: | .#.] ;
{ØpmØ}:m <=> _ CLOSED ;
        ! pahin<> pahin<>ta (pahin<>na) pahim<p>ana pahim<p>ia
	! pahim<m>an pahim<m>issa

{sØsØ}:s <=> _ VoM [:t | .#.] ; ! koira<s> koira<s>ta
        ! koira<>an koira<>ana koira<>ina

!!!:a /<= :a :Ø* _ :Ø* :a ; ! *vapaa<>a

{Ødt}:Ø => _ a [:n | :t] ; ! muu<>an (muu<d>an) muu<t>aman

{Ødt}:d => _ CLOSED ; ! muu<d>an

{Ødt}:t /<= _ CLOSED ; ! muu<t>amassa *muu<t>atta *muu<t>an

{tntts}:s <=> _ VoØ :i ;
{tntts}:t => _ OPEN , _ VoØ [{Øt}:|.#.] ;
{tntts}:n => _ CLOSED , _ VoØ t ; ! tunan<n>ne tuha<n>ten
    ! tuhan<s>ia tuhan<n>en tuha<n>ten tuha<t> tuha<t>ta tuhan<t>ena

{snttns}:s <=> _ VoØ [{ij}: | .#.] ;
{snttns}:n => _ [CLOSED | VoØ {nrs}:] ;
{snttns}:t => _ OPEN , _ VoØ {Øt}: ;
    ! kandeksan<s>ia kahdeksan<n>en (kahdeksan<n>na) kahdeksan<t>ena
    ! kahdeksan<n>nen (kahdeksan<n>na) kahdeksan<n>en

{ttØ}:Ø <=> _ s VoØ [{Øt}:|:t] ;
    ! vei<t>si vei<>stä vei<t>senä vei<t>siä

! {yØ}:Ø <=> _ (ö) {ij}: ; ! <y>ö~<>öitä

{äØö}:Ø|{äØö}:ö <=> _ {ij}: ;
        ! leve<ä> leve<>itä leve<ö>iden

!!!!t e n .#. => [h:|:l|:n|:r|:s|:t] :Ø* VoØ :Ø* _ ; ! 

