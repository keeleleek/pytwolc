Co = [: .o. [b|d|h|k|l|m|n|p|r|s|t|v]] | j ;
Ci = [[Co].u .o. :] ;
CØ = [Ci - Co] ;
Vo = : .o. [a | e | i | o | u | y | ä | ö] ;
Vok = [Vo - {ij}:] ;
Vi = [[Vo].u .o. :] ;
VØ = [Vi - Vo] ;
VV = [:a  :Ø* :a|:e :Ø* :e|:i :Ø* :i|:o :Ø* :o|:u :Ø* :u|
      :y :Ø* :y|:ä :Ø* :ä|:ö :Ø* :ö] ;
VVØ = [:a :Ø* [:a & :Ø]] | [:e  :Ø* [:e & :Ø]] | [:i  :Ø* [:i & :Ø]] | [:o  :Ø* [:o & :Ø]] | [:u :Ø* [:u & :Ø]] | [:y :Ø* [:y & :Ø]] | [:ä :Ø* [:ä & :Ø]] | [:ö :Ø* [:ö & :Ø]] ;
VhV = [:a :h (:Ø) :a|:e :h (:Ø) :e|:i :h (:Ø) :i|:o :h (:Ø) :o|:u :h (:Ø) :u|:y :h (:Ø) :y|:ä :h (:Ø) :ä|:ö :h (:Ø) :ö] ;
VbV = [:e :a|:e :ä|:i :a|:i :o|:i :ö|:o :a|:u :a|:u (:Ø) :e|:y (:Ø) :e] ;
Sy = [Vo+ [Co|CØ]+] ;
!CLOSED = Vi ({ij}:i) Ci (VØ) [Ci | END] ; 
!CLOSED = Vi ({ij}:i|{ØiØ}:) Ci (VØ) [Ci | END] ;
CLOSED = Vi ({ij}:i|{ØiØ}:) Ci [(VØ) Ci |:Ø* END] ;
!OPEN = Vo ((VØ) {ij}:i|{ØiØ}:) [Ci Vo|END] ;
OPEN = Vo ((VØ) {ij}:i|{ØiØ}:) [Ci Vo|:Ø* END] ;


{V}:a <=> :a :Ø* ({Øh}:) _ ; ! maah<a>n
{V}:e <=> :e :Ø* ({Øh}:) _ ; ! nalle<e>n
{V}:i <=> :i :Ø* ({Øh}:) _ ; ! risti<i>n
{V}:o <=> :o :Ø* ({Øh}:) _ ; ! valo<o>n
{V}:u <=> :u :Ø* ({Øh}:) _ ; ! parru<u>n
{V}:y <=> :y :Ø* ({Øh}:) _ ; ! kyky<y>n
{V}:ä <=> :ä :Ø* ({Øh}:) _ ; ! pääh<ä>n
{V}:ö <=> :ö :Ø* ({Øh}:) _ ; ! yöh<ö>n

{aØ}:Ø <=> _ [{ij}: | t] ; ! ma<a> ma<>issa, koir<>ia, jumal<>ten
{ao}:o <=> _ {ij}: ; ! kair<a> kair<o>ja 
{aØo}:o => _ [{ij}:j | {ij}:i :Ø* Co] ; ! perun<o>ita
{aØo}:Ø => _ {ij}:i ; ! perun<>ia ** toghether with GEN & PTV 
{aØo}:a /<= _ {ij}: ; ! perun<a>

{aä}:a <=> [:a|:o|:u] :* _ ; ! maass<a>, piiss<ä>

{ØaØØØ}:Ø|{ØäØØØ}:Ø =>
    [:h|:n|:t|{sØh}:|:n {ØpmØ}:Ø] _
        ({nØØØ}:) [{Øt}:|:t|{nrs}:] ,
    _ ({nØØØ}:Ø) [{ij}:|END] ;
{ØaØØØ}:a|{ØäØØØ}:ä /<=
    [:h|:n|:t|{sØh}:|:n {ØpmØ}:Ø] _
        ({nØØØ}:) [{Øt}:|:t] ,
    _ ({nØØØ}:Ø) [{ij}:|END] ;
    ! pahin<> pahimm<a>n pahin<>ta (pahin<>na)
    ! muudan<> muutam<a>n muuat<>ta (muuan<>na)
    ! saapa<>s saappa<a>n saapa<>sta
    ! vieras<> viera<a>n vierah<a>n vieras<>ta (vieras<>na)
    ! lämmin<> lämpim<ä>n lämmin<>tä (lämmin<>nä) lämpim<>iä
    ! kevät<> kevä<ä>n kevät<>tä (kevän<>nä) kevä<>itä

{eeØ}:Ø <=> _ [{ij}:] ;
    ! leve<e> leve<e>tä leve<>itä
    ! kame<e> kame<e>ta kame<>ita

{ØeØØØ}:e /<= _ [:t|{ij}:|END] ;
{ØeØØØ}:Ø =>  _ [:t|{ij}:|END] , Vo (CØ) [:s|:n] _ {nrs}: ;
    ! ahven<> ahven<e>n ahven<>ta ahven<e>na ahven<>na
    ! airut<> airu<e>n airut<>ta airu<e>na
    ! terve<> terve<e>n tervet<>tä terve<e>nä terveh<e>nä terven<>nä

{eeØØ}:Ø => _ {nØØØ}:Ø [{ij}:|:t|{nrs}:] ;
{eeØØ}:e /<= _ {nØØØ}:Ø [{ij}:|:t] ;
    ! hevon<e>n hevos<e>n hevos<>ta hevos<e>na hevos<>sa hevos<>ia

{Øe}:Ø <=> :h (:Ø) Vo s e _ ;
    ! kaunihise<>n vaatteese<e>n vaattehese<>n vaattehisi<>n 

{ØeØeØ}:Ø <=> _ END|{ij}:|:t ;
    ! vastaus<> vastauks<e>n vastaus<>ta vastauks<e>na vastauks<>ia
    ! uistin<> uistim<e>n uistin<>ta uistim<>ia
    ! askel<> askel<e>n askel<>ta askel<e>na askel<>ia
    ! sisar<> sisar<e>n sisar<>ta sisar<e>na sisar<>ia
    ! tuha<>t tuhann<e>n tuhat<>ta tuhant<e>na tuhans<>ia
    ! kalleus<> kalleud<e>n kalleut<>ta kalleut<e>na kalleuks<>ia

{Øh}:Ø => Co Vo ({nØØØ}:Ø) _ , VbV _ , Co VØ ({nØØØ}:Ø) {ij}:i _ ;
    ! talo<>on valtio<>on hevose<>en hevosi<>in

{ØØh}:h => o _ [:o [:n|:s]|VØ {ij}: [:s|:t|Vo Co|{Øt}:Ø :a]] ,
           a _ [:a [:n|:s]|VØ {ij}: [:s|:t|Vo Co|{Øt}:Ø :a]] ,
	   e _ [:e [:n|:s]|VØ {ij}: [:s|:t|Vo Co|{Øt}:Ø :a]] ,
	   a _ CØ [:a [:n|:s]|VØ {ij}: [:s|:t|Vo Co|{Øt}:Ø :a]] ;
    ! vapa<>a vapa<h>an vapa<h>isin
    ! tieno<>o tieno<h>on tieno<h>isin
    ! ??korke<>e tieno<>o koira<h>an
    ! NOTE: some overgenerated forms remain

{Øth}:h => _ [:e|:i|:u] [:n|:s],_ VØ {ij}:i [e|s|:t|{Øt}: {aä}:];
{Øth}:t <=> _ VØ [{Øt}:|:t] ;
    ! vaatte<h>en vaatte<h>esen vaatte<h>ia vaatte<h>isin
    ! ori<h>in ori<h>isen
    ! kiiru<t>ta kiiru<h>un kiiru<h>isin
    ! vaate<t>ta ori<t>ta
    ! askele<> askele<t>ta

{ØhØtØØ}:h /<= _ ;
{ØhØtØØ}:t => {ØeØeØ}:Ø {Øt}:t {aä}: _ ;
{ØhØtØØ}:Ø => _ :{ØeØeØ}:Ø END|{ØeØeØ}:e|{ØeØeØ}:Ø {ij}:
    ! askele<> askele<t>ta askele<>na askele<>ita

{iieØ}:Ø <=> _ {ij}: e ;
{iieØ}:e => _ [{ij}:|:i] ;
{iieØ}:i /<= _ [{ij}:|i] ;
    ! paperi<i> paperi<i> paper<e>j paper<>ien

{ØieØ}:Ø <=> _ :Ø* END ,
        _ {ij}: e ;
{ØieØ}:e => _ [{ij}:|i] ;
    ! kalsium<> kalsium<i>a kalsium<e>ja kalsium<>ien

{ieeeØ}:i <=> _ :Ø* END ;
{ieeeØ}:Ø <=> _ {ij}: ;
    ! tupp<i> tup<e>n tupp<e>a tupp<e>na tupp<>ia tupp<>ien

{ieeØeØ}:i <=> _ :Ø* END ;
{ieeØeØ}:Ø => _ {ij}: ,
        [:h :Ø*|:l|:n|:r|Vo :Ø* :s :Ø*|:t] _ [{Øt}:|:t] ;
{ieeØeØ}:e /<= _ {ij}: ;
{ieeØeØ}:e /<= [:l|:n|:r|Vo :s|:t] _ [{Øt}:|:t] ;
    ! niem<i> niem<e>n niem<e>ä nien<>tä niem<e>nä niem<>iä
    ! lum<i> lum<e>n (lum<e>a) lun<>ta lum<e>na lum<>ia
    ! suks<i> suks<e>n suks<e>a (sus<>ta) suks<e>na suks<>ia

{ieØeØ}:i <=> _ :Ø* END ;
{ieØeØ}:Ø <=> _ [{ij}: | :t] ;
    ! käs<i> käd<e>n kät<>tä kät<e>nä
    ! loh<i> loh<e>n loh<>ta loh<e>na loh<>ia

{ieØeØØ}:i <=> _ END ;
{ieØeØØ}:Ø => _ [{ij}: | :t] ,
        Vo (CØ) [:s|:r|:n] _ {nrs}: ;
{ieØeØØ}:e /<= _ [{ij}:|:t] ,
        Vo (CØ) Co _ {Øt}: ;
    ! pien<i> pien<e>n pien<>tä pien<e>nä pien<>nä pien<>iä
    ! laps<i> laps<e>n las<>ta laps<e>na las<>na
    ! haps<i> haps<e>n haps<e>a has<>ta haps<e>na has<>na haps<>ia
    ! nuor<i> nuor<e>n nuor<>ta nuor<e>na (nuor<>na nuor<>ra) nuor<>ia


{iØ}:Ø <=> _ (e) {ij}: ,
        :h Vo s e _ ,
	:h (:Ø) VØ {ij}:i s i _ ;
    ! t<i>e t<>eitä
    ! kame<e> kame<>ita
    ! vapaisi<i>in vapahisi<>n

{ØiØ}:i => _ :n VØ [{nrs}: | :t | END ] ;
    ! onneto<>n onneto<i>n onneto<>nta onneto<i>nta onneto<i>nna

{ØiØiØ}:Ø => _ {ij}:|{nrs}:|:t|END ;
{ØiØiØ}:i /<= _ {ij}:|:t|END ;
    ! kauris<> kauri<i>n kauri<>sta kauri<i>na kauri<>sna kauri<>ita
    ! kauri<i>seen kaurih<i>sen kauri<i>ta kauri<>hia

{ij}:i => Co :Ø* _ , _ :Ø* Co ;
{ij}:j => Vo _ :Ø* Vo ;
    ! talo<i>ssa talo<j>a

{kØ}:Ø <=> _ CLOSED ;
    ! ark<k>u ark<>un

{kØØ}:Ø | {tØØ}:Ø => _ CLOSED ,
        _ [:o|:u|:y|:ö] {ij}:i [:t|:h] ;
{kØØ}:k | {tØØ}:t /<= _ CLOSED ,
        _ [:o|:ö] {ij}:i :t ;
    ! mansik<k>a mansik<>an mansik<>oita mansik<k>oja mansik<>oihin
    ! mansik<k>oihin mansik<k>oja mansik<>oita *mansik<k>oita
    ! ilmet<t>ty ilmet<t>yjä ilmet<>yitä *ilmet<t>yitä
    ! sisäk<k>kö sisäk<>öihin sisäk<k>öihin

{kh}:k <=> _ :s ;
    ! ka<k>si ka<h>den ka<h>ta

{mn}:m <=> _ :Ø* [Vo | :m | :p] ;
    ! lu<m>i lu<m>en lu<n>ta lu<m>ena lu<m>ia
    ! tuo<m>i tuo<m>en tuo<m>ea (tuo<n>ta) tuo<m>ia
    ! nie<m>i nie<m>en nie<m>eä nie<n>tä
    ! uisti<n> uisti<m>en uisti<n>ta
    ! onneto<n> onnetto<m>an onneto<n>ta onneto<n>na
    ! lämmi<n> lämpi<m>än lämmi<n>tä lämpi<m>änä lämpi<m>iä
    ! pahi<n> pahi<m>man vase<m>man seitse<m>än

{nrs}:r => :r VØ _ ; 
{nrs}:s => :s VØ ({nØØØ}:Ø) _ ;
    ! nuor<r>a nuore<n>a nuor<n>a
    ! las<n>a lapse<n>a las<s>sa

{ns}:n <=> _ :e :n ;
    ! hevo<n>en hevo<s>en hevo<s>ta hevo<s>ena hevo<s>na hevo<s>ia

{oØ}:Ø <=> _ {ij}: ;
    ! tieno<o> tieno<o>ta tieno<>ita aino<o>

{pØ}:Ø|{pm}:m <=> _ CLOSED ;
    ! tup<p>i tup<>en saap<>as saap<p>aan
    ! läm<m>in läm<p>imän läm<m>intä

{pØp}:Ø <=> _ s {ieØeØØ}:Ø [{nrs}: | :t] ;
    ! la<p>si la<p>sen la<>sta la<p>sena la<>sna la<>ssa la<p>sia
    ! ha<p>si ha<p>sen ha<>sta ha<p>sena ha<>sna ha<>ssa ha<p>sia

{shs}:s => _ VØ [{nrs}:|{Øt}:|:t|END] ;
{shs}:h /<= _ VØ [{Øt}:|:t|END] ;
    ! mie<s> mie<h>en mie<s>tä mie<h>enä mie<h>iä

{sØh}:s <=> _ VØ [:t | {nrs}: | END] ;
{sØh}:h => _ Vo [:n|:s] ,
        _ VØ {ij}:i [(:t :t) e n|{Øt}:Ø {aä}:|s] ;
    ! viera<s> viera<>an (viera<h>an) viera<s>ta
    ! viera<>ana (viera<s>na) (viera<h>ana) (viera<h>isin)
    ! uro<s> uro<>on uro<h>on uro<s>ta uro<>oseen uro<h>osen uro<s>ten

{tØ}:Ø <=> _ CLOSED ;
    ! pat<t>i pat<>in 

{td}:d <=> _ CLOSED ;
    ! va<t>i va<d>in

{tds}:s|{tdsØ}:s|{tls}:s|{trs}:s|{tns}:s <=> _ (VØ) [:i|:Ø* END] ;
{tds}:d|{tdsØ}:d|{tls}:l|{tns}:n|{trs}:r <=> _ :e Ci (VØ) [Ci|:Ø*END] ;
    ! to<s>i kä<s>i kä<s>iä kak<s>i jäl<s>i kan<s>i kahdeksa<s>
    ! kä<d>en kä<d>essä kah<d>en jäl<l>en kan<n>essa
    ! kan<s>i kan<s>ia kan<n>en kan<t>ta kan<t>ten kan<s>ia
    ! hir<s>i hir<t>enä hir<r>essä hir<s>iä

{tdsØ}:Ø <=> _ VØ :t ;
    ! kah<>ta

{ØØthn}:n => _ VØ {nrs}: ;
{ØØthn}:t <=> _ VØ [:t] ;
    ! terve<> (terve<h>en) (terve<n>nä) terve<t>ä (terve<t>ten)

{tØthn}:t <=> _ VØ [:t | END] ;
{tØthn}:n => _ VØ {nrs}: ;
{tØthn}:h => _ [Vo-{ij}:] ,
             _ VØ {ij}:i [e|:s|:t|e|{Øt}:Ø {aä}:] ;
{tØthn}:Ø => _ VØ {ij}: , _ Vo ;
    ! kuollu<t> kuollu<t>ta (kuollu<t>ten) kevä<t>
    ! (kuollu<n>na) (kevä<n>nä)
    ! kevä<h>än kuolle<h>en kevä<h>änä (kevä<h>itten) kuolle<h>isin
    ! kevä<>än kevä<>änä kuolle<>en kuolle<>ena
    ! kevä<>itä kevä<>isiin kuolle<>ita kuolle<>itten
    ! airu<h>itten airu<h>ien (airu<h>isin)
    ! äity<h>itten äity<h>ien (äity<h>isin)

{uØ}:Ø <=> _ (o) {ij}: ;
    ! s<u>o s<>oita tenkkapo<o> tenkkapo<>ita

{ue}:u <=> _ [:t|:n] ;
    ! kuoll<u>t kuoll<u>tta kuoll<u>nna

{ØuØuØ}:Ø <=> _ [:t|{ij}:|:Ø* END] ;
    ! kiiru<> kiiru<u>n kiirut<>ta kiiru<u>na kiiruh<u>na

{Øt}:t => Vok (:Ø) VØ {ij}:| Vok (:Ø) Vok ({ij}:) _ ,
        VØ Vok {ij}:i _ ,
        {ieeØeØ}:Ø|{ieØeØØ}:Ø|{ieØeØ}:Ø|{ieØeØØ}:Ø|{ØiØiØ}:Ø|
            {ØeØeØ}:Ø|{ØeØØØ}:Ø|{eeØØ}:Ø {nØØØ}:Ø|
	    {ØaØØØ}:Ø|{ØäØØØ}:Ø|
	    {ØuØuØ}:Ø _ ,
	{dt}:t _ ,
	BEGIN Ci* [Vok+ Ci+]+ Vok+ Ci+ [:a|:ä] _ ,
	BEGIN Ci* [Vok+ Ci+]+ Vok+ Ci+ Vok {ij}:i _ ;
    ! maa<t>a mai<t>a ainoo<t>a ainoi<t>a autio<t>a autioi<t>a
    ! soi<t>a töi<t>ä tei<t>ä
    ! tuoh<t>a loh<t>a lun<t>a nien<t>tä pien<t>ä nuor<t>ta
    ! kät<t>ä tot<t>a hirt<t>tä jält<t>ä kant<t>ta
    ! las<>ta has<t>a veis<t>ä peis<t>ä sus<t>ta us<t>a
    ! sisar<t>a uistin<t>a lämmin<t>ä vasen<t>ta
    ! hevos<t>a vastaus<t>a
    ! vieras<t>a saapas<t>a
    ! arvelui<t>a ilmetyi<t>ä kantaja<t>a pasuuna<t>a

{Øt}:Ø => Co Vo (CØ) ({ij}:j) _ ,
        Co (CØ) VØ (CØ) {ij}:i _ ,
	:e :a|:e :ä|:i :o|:i :ö|:o :a|:u :a _ ,
        {dt}:d _ ;
    ! arkku<>a arkkuj<>a pato<>a patoj<>a seitsem<>ä
    ! arvelu<>a arveluj<>a ilmetty<>ä ilmettyj<>ä
    ! sisäkkö<>ä sisäkköj<>ä
    ! ahveni<>a
    ! maid<>en

STOP
{Øt}:Ø /<= [{ieØe}:Ø|{ieØØ}:Ø|{ØeØe}:|
            {ØeØeØ}:|{ØaØØ}:Ø|{ØäØØ}:Ø] ({ØnØ}:Ø|{nØØ}:Ø) _ ;
!{Øt}:Ø /<= [VV |VVØ {ij}:| Vo Vo {ij}:] _ ;
    ! *maa<>a maa<t>a *ainoo<>a ainoo<t>a
    ! *autioj<>a autioi<t>a
{Øt}:Ø /<= [{uØ}: o|{iØ}: e] ({ij}:) _ ;
    ! *suo<>a suo<t>a *soi<>a soi<t>a tei<t>ä
{Øt}:t /<= Co [:o|:u|:y|:ö|{iie}:|{Øiie}:|{iie}:|:e] _ ;
    ! *talo<t>a *risti<t>ä valo<>a valoj<>a *arvelu<t>a
    ! lovi<>a hevosi<>a, (autio<>a) herttua<>a 
{Øt}:t /<= BEGIN Co* Vo+ [Co|CØ]+ [{aØ}:a|{ao}:a] _ ;
    ! *koira<t>a *kissa<t>a 
{Øt}:t /<= BEGIN Co* Vo+ [Co|CØ]+ [Vo|VØ] {ij}: _ ;
    ! *arkkui<t>a arkku<>ja *vatei<t>a vatej<>a *käsi<t>ä käsi<>ä
    ! (kantaji<t>a) *valoi<t>a valoj<>a 
{Øt}:t /<= Co VØ ({ØnØ}:Ø|{nØØ}:Ø) {ij}: _ ;
    ! *hevosi<t>a hevosi<>a
    ! vapaa<t>a ainoo<t>a
    ! arvelui<t>a loh<t>a autio<t>a, hevos<t>a sisar<t>a,
    ! autio<t>a leveä<t>ä, kulkija<t>a seitsemä<t>ä
START

s e {Øe}: n => [VV | VhV] _ :Ø* END ;
    ! ainoo<seen> vapaa<seen> korkee<seen>
    ! (vapaha<sen>) (koiraha<sen>)

{ij}:i s i {iØ}: n => [Vo (:h|:Ø) (:Ø) VØ] _ ;
    ! vapa<isiin> aino<isiin> (ainoh<isin>)
    ! saappa<isiin> (saappah<isin>) koira<isiin> (koirah<isin>

{ij}: e n /<= VVØ _ ;
    ! valo<jen> *tie<jen> *maa<jen> käs<ien> arvelu<jen>

{ij}: e n /<= Vo :Ø* VØ _ ;
    ! valo<jen> käs<ien> *te<jen> ma<jen> 

{ij}: {dt}: /<= [Co - :h] :Ø* _ ;
    ! *käs<iden> *koir<iden>

{ij}: {dt}: /<= BEGIN Co* Vo+ [Co|CØ]+ Vo _ ;
    ! *valo<iden> *kisso<iden>

{dt}:t <=> _ :t ;
    ! mai<d>en mai<t>ten

t e n /<= Vo :Ø* _ :Ø* END ;
    ! *arvelu<ten> risti<ten> *ma<ten> *tie<ten>

i n /<= [VV] _ :Ø* END ;
    ! (valo<in>) (autio<in>) (vate<in>)
    ! *maa<in> *tie<in> *suo<in>

STOP

****************************************

{ØaØa}:Ø => _ [{ij}: | :t |END] ; ! koiras<> koira<a>n koiras<>ta
{ØaØa}:a /<= _ [{ij}:|END] , :s _ ;
        ! koira<>issa koiras<>ta vasen<> vasemp<a>ta

{ØiØØ}:Ø =>  _ [{ij}: | :t | END] , :s _ {nrs}: ;
{ØiØØ}:i /<=  _ [{ij}: | :t | END] ;
        ! kauris<> kauris<>ta (kauris<>na)
	! kauri<i>na  kaurih<i>n

{ØiØi}:Ø <=> _ [:t | {ij}: | END] ;
        ! ori<> ori<i>n orit<>ta ori<i>na ori<>ita

{iiee}:e => _ {sØh}: VØ {ij}: ;
        ! kaun<e>issa (kaun<e>hien)
        ! kaun<i>s kaun<i>in kaun<i>sta kaun<i>ina

{iiØie}:Ø <=> _ [:t|{ij}: e] ; ! (sankar<>ten) sankar<>ien
{iiØie}:e => _ [:i|{ij}:]; ! sankar<e>ita (sankar<e>in)
{iiØie}:i /<= _ [:i|{ij}:]; ! sanka<i> sankar<i>a sankar<i>na

{kkØ}:Ø <=> _ s VØ :t ;
        ! su<k>si su<k>sea su<>sta u<k>si u<>sta

{ØkØ}:Ø <=> _ s VØ [:t | END] ;
        ! vastau<>s vastau<k>sen vastau<>sta vastau<k>sena

{ØØk}:k <=> _ :s VØ {ij}: ;
        ! kalleu<>s kalleu<>den kalleu<>tta kalleu<k>sia

{ØkØØ}:Ø <=> _ s VØ [:t | {nrs}: | END]; ! koira<>sta (koira<>sna)
        ! koira<k>sen koira<k>sena koira<k>sia

{mnt}:n <=> _ VØ [{nrs}: | END] ;
{mnt}:t <=> _ VØ :t ;
        ! muua<t>ta muua<n> (muua<n>na)

{ØnØ}:Ø <=> _ :t VØ [:t|END], _ :n VØ :t, _ [:n|:s] VØ [{nrs}:|END];
        ! tuha<>t tuha<n>en tuha<>tta tuha<n>tena tuha<n>sia
	! kahdeksa<>s kahdeksa<n>nen kahdeksa<>tta khdeksa<n>tena

{nØØ}:n <=> _ END ; ! hevone<n> hevose<>ssa seitsemä<n> seitsemä<>ssä

{nss}:n <=> _ :e :n ; ! hevo<n>en hevo<s>en hevo<s>ta hevo<s>ia

{ØoØo}:Ø => _ [:t|{ij}:|:i|END] ;
{ØoØo}:o /<= _ [:t|{ij}:|:i|END] ;
        ! uro<>s uro<>sta uro<o>n uro<o>na (uroh<o>na)

{Øpm}:Ø <=> _ VØ [:t | END] ;
{Øpm}:m <=> _ CLOSED ;
        ! vasen<> vasen<>ta vasem<p>ana vasem<p>ia vasem<m>an vasem<m>issa

{ppØ}:p <=> _ s :Ø* Vo ;
        ! la<p>si la<p>sen la<>sta la<p>sia

{ØpmØ}:Ø <=> _ VØ [:t | {nrs}: | END] ;
{ØpmØ}:m <=> _ CLOSED ;
        ! pahin<> pahin<>ta (pahin<>na) pahim<p>ana pahim<p>ia
	! pahim<m>an pahim<m>issa

{sØsØ}:s <=> _ Vi [:t | END] ; ! koira<s> koira<s>ta
        ! koira<>an koira<>ana koira<>ina

!!!:a /<= :a :Ø* _ :Ø* :a ; ! *vapaa<>a

{Ødt}:Ø => _ a [:n | :t] ; ! muu<>an (muu<d>an) muu<t>aman

{Ødt}:d => _ CLOSED ; ! muu<d>an

{Ødt}:t /<= _ CLOSED ; ! muu<t>amassa *muu<t>atta *muu<t>an

{tntts}:s <=> _ VØ :i ;
{tntts}:t => _ OPEN , _ VØ [{Øt}:|END] ;
{tntts}:n => _ CLOSED , _ VØ t ; ! tunan<n>ne tuha<n>ten
    ! tuhan<s>ia tuhan<n>en tuha<n>ten tuha<t> tuha<t>ta tuhan<t>ena

{snttns}:s <=> _ VØ [{ij}: | END] ;
{snttns}:n => _ [CLOSED | VØ {nrs}:] ;
{snttns}:t => _ OPEN , _ VØ {Øt}: ;
    ! kandeksan<s>ia kahdeksan<n>en (kahdeksan<n>na) kahdeksan<t>ena
    ! kahdeksan<n>nen (kahdeksan<n>na) kahdeksan<n>en

{ttØ}:Ø <=> _ s VØ [{Øt}:|:t] ;
    ! vei<t>si vei<>stä vei<t>senä vei<t>siä

! {yØ}:Ø <=> _ (ö) {ij}: ; ! <y>ö~<>öitä

{äØö}:Ø|{äØö}:ö <=> _ {ij}: ;
        ! leve<ä> leve<>itä leve<ö>iden

!!!!t e n END => [h:|:l|:n|:r|:s|:t] :Ø* VØ :Ø* _ ; ! 

